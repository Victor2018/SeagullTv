apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    signingConfigs {
        release {
            storeFile file('../docs/victor_keystore.jks')
            storePassword "android"
            keyAlias "victor"
            keyPassword "android"
            v2SigningEnabled false  //禁用v2签名方式
        }
    }

    defaultConfig {
        applicationId "com.victor.seagull"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 20
        versionName "1.2.0"

        ndk {
            moduleName "ImageBlur"
            ldLibs "m", "log", "jnigraphics"
//            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    //移除lint检测的error
    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
        overseas {
            res.srcDirs = ["overseas/res"]
        }
    }

    productFlavors {
        main {
            // 每个环境包名不同
            applicationId "com.victor.seagull"
            // 动态修改 常量 字段
            buildConfigField "String", "AUTHORITY", '"content.video.content"'
            manifestPlaceholders = [UPDATE_URL_KEY: "https://raw.githubusercontent.com/Victor2018/SeagullTv/master/docs/update_json.txt",
                                    AUTHORITIES:"content.video.content"]
        }
        overseas {
            // 每个环境包名不同
            applicationId "com.victor.overseas.seagull"
            // 动态修改 常量 字段
            buildConfigField "String", "AUTHORITY", '"content.overseas.video.content"'
            manifestPlaceholders = [UPDATE_URL_KEY: "https://raw.githubusercontent.com/Victor2018/SeagullTv/master/docs/update_json.txt",
            AUTHORITIES:"content.overseas.video.content"]
        }
    }

    // 自定义输出配置，这里我们加上APK版本号1.0
    applicationVariants.all { variant -> variant.outputs.each { output -> def outputFile = output.outputFile
        if (outputFile != null && outputFile.name.endsWith('.apk')) {
            String typeName = variant.buildType.name;
            //只处理Rlease版本
            if ("release".equals(typeName)) {
                // 输出apk名称为SeagullTv_v1.0_main.apk
                def fileName = "SeagullTv_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.android.support:cardview-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'jp.wasabeef:glide-transformations:1.3.1'
    compile 'com.mikepenz:iconics-core:2.5.5@aar'
    compile 'com.mikepenz:material-design-iconic-typeface:2.2.0.1@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.5.0.1@aar'
    compile 'com.mikepenz:foundation-icons-typeface:3.0.0.1@aar'

    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.squareup.okhttp3:okhttp:3.1.2'
    compile 'com.google.code.gson:gson:2.6.1'
    compile 'com.alibaba:fastjson:1.1.56.android'

    compile('com.afollestad.material-dialogs:core:0.9.0.1@aar') {
        transitive = true
    }
    compile('com.afollestad.material-dialogs:commons:0.9.0.1') {
        transitive = true
    }
    compile files('libs/jsoup-1.9.2.jar')
    compile 'com.github.Victor2018:AppUpdateLib:v1.7'
    compile 'com.github.Victor2018:PermissionHelper:v1.0'
}